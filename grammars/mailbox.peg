// Mailbox grammar
// This is the [rfc5322](./rfc5322.peg) grammar treeshaken to exclude everything but mailbox
// See [mailbox-computed](./mailbox-computed.peg) for the actual grammar used with the lib

mailbox         :=    name_addr | addr_spec
name_addr       :=    display_name? angle_addr
addr_spec       :=    local_part '@' domain
display_name    :=    phrase
angle_addr      :=    CFWS? '<' addr_spec '>' CFWS? | obs_angle_addr
local_part      :=    dot_atom | quoted_string | obs_local_part
domain          :=    dot_atom | domain_literal | obs_domain
phrase          :=    word+ | obs_phrase
CFWS            :=    { FWS? comment }+ FWS? | FWS
obs_angle_addr  :=    CFWS? '<' obs_route addr_spec '>' CFWS?
dot_atom        :=    CFWS? dot_atom_text CFWS?
quoted_string   :=    CFWS? DQUOTE { FWS? qcontent }* FWS? DQUOTE CFWS?
obs_local_part  :=    word { '\.' word }*
domain_literal  :=    CFWS? '\[' { FWS? dtext }* FWS? '\]' CFWS?
obs_domain      :=    atom { '\.' atom }*
word            :=    atom | quoted_string
obs_phrase      :=    word { word | '.' | CFWS }*
FWS             :=    { WSP* CRLF }? WSP+ |  obs_FWS
comment         :=    '\(' { FWS? ccontent }* FWS? '\)'
obs_route       :=    obs_domain_list ':'
dot_atom_text   :=    atext+ { '\.' atext+ }*
DQUOTE          :=    '\x22'              // double quote, i.e. '"'
qcontent        :=    qtext | quoted_pair
dtext           :=    '[\x21-\x5a]' | '[\x5e-\x7e]' | obs_dtext
atom            :=    CFWS? atext+ CFWS?
WSP             :=    SP | HTAB           // white space
obs_FWS         :=    WSP+ { CRLF WSP+ }*
CRLF            :=    '\r\n'              // Internet standard newline
ccontent        :=    ctext | quoted_pair | comment
obs_domain_list :=    { CFWS | ',' }* '@' domain { ',' CFWS? { '@' domain }? }*
atext           :=    '[A-Za-z0-9!#$%&\x27\*\+\-\/=?^_`{|}~]'
qtext           :=    '\x21' | '[\x23-\x5b]' | '[\x5d-\x7e]' | obs_qtext
quoted_pair     :=    '\\' { VCHAR | WSP }  | obs_qp
obs_dtext       :=    obs_NO_WS_CTL | quoted_pair
SP              :=    '\x20'              // space, i.e. 'Space'
HTAB            :=    '\x09'              // horizontal tab, i.e. 'TAB'
ctext           :=    '[\x21-\x27]' | '[\x2a-\x5b]' | '[\x5d-\x7e]' | obs_ctext
obs_qtext       :=    obs_NO_WS_CTL
obs_qp          :=    '\\' { '\x00' | obs_NO_WS_CTL | LF | CR }
VCHAR           :=    '[\x21-\x7E]'       // visible (printing) characters
obs_NO_WS_CTL   :=    '[\x01-\x08]' | '\x0B' | '\x0C' | '[\x0E-\x1F]' | '\x7F'
obs_ctext       :=    obs_NO_WS_CTL
LF              :=    '\x0A'              // linefeed, i.e. '\n'
CR              :=    '\x0D'              // carriage return, i.e. '\r'